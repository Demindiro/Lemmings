-- Memory allocator for allocating contiguous physical memory.
--
-- This allocator requires physical memory to be identity mapped to a fixed location.
door Allocator {
	-- Get the base of the identity mapping.
	--
	-- To translate the physical address, simply add the physical address to the returned pointer.
	identity_base = Void -> IdentityBase
	-- Allocate a physically contiguous range with the given alignment.
	--
	-- Generally, this variant should be preferred over [`alloc32`].
	alloc64 = Alloc64 -> MaybeRegion64
	-- Allocate a physically contiguous range with the given alignment.
	--
	-- This variant is intended for usecases that require physical memory within the first 4GiB.
	alloc32 = Alloc32 -> MaybeRegion32
	-- Release an allocation.
	free = Free -> Void
}

record Void {
}

u8 Byte = 0 to 0xff
pointer IdentityBase = shared Byte

-- 64-bit physical address
--
-- While 0 is usually valid as physical address,
-- it is still forbidden to allocate to avoid confusing incidents.
u64 Phys64 = 1 to 0xffff_ffff_ffff_ffff
u32 Phys32 = 1 to 0xffff_ffff

u64 NoPhys64 = 0
u32 NoPhys32 = 0

u64 Len64 = 0 to 0xffff_ffff_ffff_ffff
u32 Len32 = 0 to 0xffff_ffff

-- 64-bit region
--
-- This returns the real amount of memory allocated,
-- which may be larger than the requested amount.
--
-- Applications are free to either use or ignore it.
record Region64 {
	base = Phys64
	len = Len64
}
record Region32 {
	base = Phys32
	len = Len32
}

record NoRegion64 {
	base = NoPhys64
	len = Len64
}
record NoRegion32 {
	base = NoPhys32
	len = Len32
}

sum MaybeRegion64 = Region64 | NoRegion64
sum MaybeRegion32 = Region32 | NoRegion32

u8 Align64 = 0 until 64
u8 Align32 = 0 until 32

record Alloc64 {
	len = Len64
	align = Align64
}
record Alloc32 {
	len = Len32
	align = Align32
}

record Free {
	base = Phys64
	len = Len64
}
