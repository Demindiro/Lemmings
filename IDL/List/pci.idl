-- This interface exposes *local* PCI devices.
--
-- Given that PCI devices make extensive use of MMIO,
-- it is assumed any direct use of such devices will occur
-- on a system with a shared memory bus.
-- Hence, routines return direct pointers to these devices.
--
-- The primary purpose of this interface is to:
-- - Efficiently enumerate all PCI devices in the system
-- - Enforce exclusive access to devices.
-- - Help set up interrupts (abstracting out platform-specific details)
-- - Resize/remap BARs
door Pci {
	-- Get the base address of the PCI configuration region,
	-- as well as segment group and bus start/end.
	configuration = Void -> Configuration

	-- Acquire exclusive access to a device.
	acquire_dev = Bdf -> AcquireResult

	-- Release exclusive access to a device.
	release_dev = Bdf -> Void

	-- Register the current thread for an MSI.
	--
	-- The returned address and value should be programmed in the device.
	register_msi = Void -> MaybeMsi

	-- Unregister the thread for an MSI.
	unregister_msi = Msi -> Void

	-- Allocate 32-bit MMIO space.
	--
	-- This space can be used for mapping BARs.
	allocate_mmio32 = AllocateMmio32 -> MaybeAddr32

	-- Allocate 64-bit MMIO space.
	--
	-- This space can be used for mapping BARs.
	allocate_mmio64 = AllocateMmio64 -> MaybeAddr64

	-- Free MMIO space.
	release_mmio = Addr64 -> Void
}

record Void {
}

record Configuration {
	base = ConfigurationBase
	segment_group = SegmentGroup
	bus_start = BusNumber
	bus_end = BusNumber
}

pointer ConfigurationBase = shared PciFunction
u16 SegmentGroup = 0 to 0xffff
u8 BusNumber = 0 to 0xff

record PciFunction {
}

u16 Bdf = 0 to 0xffff

u8 GotLock = 0
u8 AlreadyLocked = 1
sum AcquireResult = GotLock | AlreadyLocked

record Msi {
	address = MsiAddress
	data = MsiData
}
record NoMsi {
	address = NoMsiAddress
	data = MsiData
}
sum MaybeMsi = Msi | NoMsi

u64 NoMsiAddress = 0
u64 MsiAddress = 1 to 0xffff_ffff_ffff_ffff

u32 MsiData = 0 to 0xffff_ffff

u8 AllocateMmio32 = 4 to 31
u8 AllocateMmio64 = 4 to 63
u32 Addr32 = 1 until 0xffff_ffff
u64 Addr64 = 1 until 0xffff_ffff_ffff_ffff
u32 NoAddr32 = 0
u64 NoAddr64 = 0
sum MaybeAddr32 = NoAddr32 | Addr32
sum MaybeAddr64 = NoAddr64 | Addr64
